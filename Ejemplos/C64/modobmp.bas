10 REM INICIALIZAR PROGRAMA
20 PRINT "{147}LOADING..."
30 POKE 53280,12     : REM COLOR BORDE
40 POKE 53281,0      : REM COLOR FONDO
50 POKE 646,1        : REM COLOR CURSOR
60 R%(0)=PEEK(56576) : REM PUERTO A CIA2
70 R%(1)=PEEK(53265) : REM REG1 VIC-II
80 R%(2)=PEEK(53270) : REM REG2 VIC-II
90 R%(3)=PEEK(53272) : REM MEM VIC-II
100 REM BORRAR BUFFER PIXELES
110 SB=23552:PB=24576:CB=55296
120 FOR I=0 TO 7999
130 POKE PB+I,0
140 PRINT "{19}LOADING...";STR$(INT(100*I/7999));"%"
150 NEXT I
160 REM MODIFICAR MODO GRAFICO
170 POKE 56576,(R%(0) AND 252) OR 2
180 POKE 53265,(R%(1) AND 223) OR 32
190 POKE 53270,(R%(2) AND 239) OR 16
200 POKE 53272,(R%(3) AND 1) OR 120
210 REM BUCLE SALVA PANTALLAS
220 X=0  : Y=0  : REM COORDENADAS
230 VX=2 : VY=1 : REM MOVIMIENTO
240 C=1  : P=1  : REM COLORES
250 REM CALCULAR POSICION PIXEL
260 CX=INT(X/8) : REM COLUMNA
270 DX=X-CX*8   : REM SUBCOLUMNA
280 CY=INT(Y/8) : REM FILA
290 DY=Y-CY*8   : REM SUBFILA
300 CP=CY*40+CX : REM CELDA
310 REM MODIFICAR BUFFER PIXEL
320 ZZ=PB+CP*8+DY
330 CB%=PEEK(ZZ)
340 IF DX>=0 AND DX<=1 THEN CB%=(CB% AND 63)+P*64
350 IF DX>=2 AND DX<=3 THEN CB%=(CB% AND 207)+P*16
360 IF DX>=4 AND DX<=5 THEN CB%=(CB% AND 243)+P*4
370 IF DX>=6 AND DX<=7 THEN CB%=(CB% AND 252)+P
380 POKE ZZ,CB%
390 REM MODIFICAR BUFFER COLOR
400 IF P=1 THEN ZZ=SB+CP : POKE ZZ,(PEEK(ZZ) AND 15)+C*16
410 IF P=2 THEN ZZ=SB+CP : POKE ZZ,(PEEK(ZZ) AND 240)+C
420 IF P=3 THEN ZZ=CB+CP : POKE ZZ,(PEEK(ZZ) AND 240)+C
430 P=P+1 : IF P>3  THEN P=1
440 C=C+1 : IF C>15 THEN C=1
450 REM MOVER COORDENADAS
460 IF X<=0   THEN X=0   : VX=2
470 IF X>=318 THEN X=318 : VX=-2
480 IF Y<=0   THEN Y=0   : VY=1
490 IF Y>=199 THEN Y=199 : VY=-1
500 X=X+VX : Y=Y+VY
510 REM COMPROBAR TECLADO
520 GET K$
530 IF K$="" THEN 260
540 REM FINALIZAR PROGRAMA
550 POKE 56576,R%(0)
560 POKE 53265,R%(1)
570 POKE 53270,R%(2)
580 POKE 53272,R%(3)